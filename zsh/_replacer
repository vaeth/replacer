#compdef replacer
_arguments -s -S : \
'(1 2 * -)'{'--help','-h'}'[print help]' \
'(1 2 * -)'{'--version','-V'}'[print version]' \
{'(--chars)-b','(-b)--chars'}'[chars (binary) mode]' \
'(--context -C '{'--before)-B+','-B)--before='}'[print specified number of lines before match]:number of lines before match:(2)' \
'(--context -C '{'--after)-A+','-A)--after='}'[print specified number of lines after match]:number of lines after match:(2)' \
'(--before -B --after -A '{'--context)-C+','-C)--context='}'[print specified number of context lines]:number of context lines:(2)' \
{'(--plain-search)-p','(-p)--plain-search'}'[use plain string for search]' \
{'(--plain-replace)-P','(-P)--plain-replace'}'[use plain string for replace]' \
{'(--encoding)-e+','(-e)--encoding='}'[use specified file encoding (binary)]:file encoding:(binary utf-8 cp850 cp1252 latin-1)' \
{'(--encoding-fallback)-E+','(-E)--encoding-fallback='}'[use specified fallback file encoding (binary)]:file encoding:(binary utf-8 cp850 cp1252 latin-1)' \
{'(--keep)-k','(-k)--keep'}'[try to keep timestamps]' \
'(2 --count -c --list -l '{'--grep)-g','-g)--grep'}'[grep mode]' \
'(2 --grep -g --list -l '{'--count)-c','-c)--count'}'[grep mode, count]' \
'(2 --grep -g --count -c '{'--list)-l','-l)--list'}'[grep mode, list files]' \
{'(--ignore-case)-i','(-i)--ignore-case'}'[ignore case]' \
{'(--multiline)-M','(-M)--multiline'}'[make \^/\$ consider each line]' \
{'(--dotall)-S','(-S)--dotall'}'[make . match newline, too]' \
{'(--unicode)-U','(-U)--unicode'}'[make \\w \\W \\b \\B follow unicode]' \
{'(--locale)-L','(-K)--locale'}'[make \\w \\W \\b \\B follow locale]' \
{'(--verbose)-X','(-X)--verbose'}'[allow comments in pattern]' \
{'(--recursive)-r','(-r)--recursive'}'[recurse if FILE is a directory]' \
{'*-s+','*--skip='}'[skip paths matching specified pattern]:path pattern:("*/")' \
{'(--colors)-H+','(-H)--colors='}'[use specified highlight mode]:highlight mode:(none dark light style)' \
{'(--yes)-y','(-y)--yes'}'[always replace (twice to always write)]' \
{'(--quiet)-q','(-q)--quiet'}'[do not print matches, quiet grep]' \
{'(--debug)-D','(-D)--debug'}'[print traceback on errors]' \
'1:search:(. "^.*$")' \
'2:replace:("\\g<0>" "\\1")' \
'*:files:_files'
